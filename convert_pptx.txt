Open your MS Power Point presentation and navigate to the "View" panel
Click on Macros
Within the Macros panel type some dummy name (like "test") and click on "Create"
The IDE (integrated development environment) Microsoft Visual Basic For Application will be opened
Within the opened IDE replace text with the code mentioned below:

Sub AddElements()

Dim shp As Shape

Dim i As Integer, n As Integer

n = ActivePresentation.Slides.Count

For i = 1 To n

    Dim s As Slide

    Set s = ActivePresentation.Slides(i)

    s.SlideShowTransition.Hidden = msoTrue

   

    Dim max As Integer: max = 0

    For Each shp In s.Shapes

        If shp.AnimationSettings.Animate = msoTrue Then

            If shp.AnimationSettings.AnimationOrder > max Then

                max = shp.AnimationSettings.AnimationOrder

            End If

        End If

    Next

    Dim k As Integer, s2 As Slide

    For k = 0 To max

        Set s2 = s.Duplicate(1)

        s2.SlideShowTransition.Hidden = msoFalse

        s2.MoveTo ActivePresentation.Slides.Count

       

        Dim i2 As Integer

        For i2 = s2.Shapes.Count To 1 Step -1

            With s2.Shapes(i2)

                If .AnimationSettings.Animate = msoTrue Then

                    If .AnimationSettings.AnimationOrder > k Then

                        .Delete

                    Else

                        .AnimationSettings.Animate = msoFalse

                    End If

                End If

            End With

        Next

    Next

Next

End Sub

Sub RemElements()

Dim i As Integer, n As Integer

Dim s As Slide

n = ActivePresentation.Slides.Count

For i = n To 1 Step -1

    Set s = ActivePresentation.Slides(i)

    If s.SlideShowTransition.Hidden = msoTrue Then

        s.SlideShowTransition.Hidden = msoFalse

    Else

        s.Delete

    End If

Next

End Sub

Go to your opened Power Point presentation, click on "Macros" and run "AddElements"

Now you can notice that the all original slides were "dimmed", and new slides were added. In newly created slides, the original slides with animations are duplicated.

Navigate to the "Save As" and select PDF as the target format
